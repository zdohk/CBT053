*          DATA SET CBT502     AT LEVEL 001 AS OF 12/15/75
*        LOCINDEX SUBROUTINE REPLACEMENT FOR MVS                        00001
*        THIS ROUTINE SUPPORTS 'LEVEL' OPTIONS OF THE 'LISTS' COMMAND   00002
*             AND IS USED BY OTHER YCC VARIATIONS OF THAT PROGRAM       00003
*        FOLLOWING IS ORIGINAL LOCINDEX DESCRIPTION:                    00004
*.....................................................................* 00005
*.                                                                   .* 00006
*.   LOCINDEX                                                        .* 00007
*.                                                                   .* 00008
*.....................................................................* 00009
*.                                                                   .* 00010
*.   1.0  GENERAL DESCRIPTION                                        .* 00011
*.                                                                   .* 00012
*.   THIS SUBROUTINE IS USED TO RETURN DSNAMES AND THE VOLSER        .* 00013
*.   FOR A SPECIFIED INDEX STRUCTURE.  THE INDEX STRUCTURE CAN BE    .* 00014
*.   SPECIFIED AS SEVERAL HIGH-LEVEL QUALIFIERS OR IT CAN BE A       .* 00015
*.   DSNAME CONTAINING ONE EMBEDDED ASTERISK NOT AS THE HIGH-LEVEL   .* 00016
*.   QUALIFIER.                                                      .* 00017
*.                                                                   .* 00018
*.....................................................................* 00019
*.                                                                   .* 00020
*.....................................................................* 00021
*.                                                                   .* 00022
*.   2.0  PARAMETER LIST AND RETURN CODE DESCRIPTION                 .* 00023
*.                                                                   .* 00024
*.   THE PARAMETER LIST IS A FOUR OR FIVE WORD LIST CONTAINING THE   .* 00025
*.   FOLLOWING:                                                      .* 00026
*.                                                                   .* 00027
*.    WORD         DESCRIPTION                                       .* 00028
*.                                                                   .* 00029
*.      1          ADDRESS OF THE 44-BYTE FIELD CONTAINING THE       .* 00030
*.                 INDEX STRUCTURE.                                  .* 00031
*.      2          ADDRESS OF THE 44-BYTE FIELD INTO WHICH           .* 00032
*.                 LOCINDEX WILL PLACE THE DSNAME.                   .* 00033
*.      3          ADDRESS OF THE 6-BYTE FIELD INTO WHICH            .* 00034
*.                 LOCINDEX WILL PLACE THE VOLSER OF THE DATASET.    .* 00035
*.      4          ADDRESS OF A 4K WORK AREA TO BE USED BY           .* 00036
*.                 LOCINDEX FOR STORING CATALOG BLOCKS.              .* 00037
*.      5          OPTIONAL FIELD FOR THE ADDRESS OF A FULLWORD      .* 00038
*.                 OF STORAGE ON AN INTEGRAL BOUNDARY INTO WHICH     .* 00039
*.                 LOCINDEX WILL STORE THE RETURN CODE.              .* 00040
*.                                                                   .* 00041
*.   THE HIGH ORDER BIT OF THE LAST WORD OF THE PARAMETER LIST       .* 00042
*.   MUST BE SET ON.                                                 .* 00043
*.                                                                   .* 00044
*.    RETURN CODE  MEANING                                           .* 00045
*.                                                                   .* 00046
*.         0       A DSNAME WAS FOUND AND THE DATASET RESIDES ON     .* 00047
*.                 A DISK VOLUME.                                    .* 00048
*.         4       THE INDEX STRUCTURE WAS NOT FOUND.                .* 00049
*.         8       A DSNAME WAS FOUND AND THE DATASET RESIDES ON     .* 00050
*.                 A TAPE VOLUME.                                    .* 00051
*.        12       A DSNAME WAS FOUND BUT IT RESIDES ON MULTIPLE     .* 00052
*.                 VOLUMES.                                          .* 00053
*.        16       THERE ARE NO MORE DATASETS FOR THIS INDEX         .* 00054
*.                 STRUCTURE.                                        .* 00055
*.        20       AN I/O ERROR WAS DETECTED ON THE CATALOG.         .* 00056
*.                                                                   .* 00057
*.....................................................................* 00058
*                                                                       00059
*        THE PRIMARY DIFFERENCES BETWEEN THE MVS VERSION                00060
*        AND THE OS VERSION ARE:                                        00061
*              1) THE PART ABOVE THE ASTERISK IS PASSED TO VSAM         00062
*                 GENERIC LOCATE AND WORKS HOWEVER GENERIC LOCATE       00063
*                 LOCATE WORKS. ONLY NON-VSAM DATASETS ARE PASSED       00064
*                 BACK TO THE CALLER.                                   00065
*              2) THE CHARACTERS BELOW THE ASTERISK MUST APPEAR         00066
*                 IN THE DATASET NAME ANYWHERE AFTER THE SEARCH         00067
*                 KEY CHARACTERS. THEY MAY CONTAIN LEADING              00068
*                 AND TRAILING PARTIAL INDEX LEVELS.                    00069
*              3) THE AMOUNT OF STORAGE REQUIRED FOR A VSAM GENERIC     00070
*                 LOCATE IS MUCH MORE THAN THE 4K AREA PROVIDED BY      00071
*                 THE OS CALLER. A 24K AREA IS GETMAINED AND ITS        00072
*                 ADDRESS IS STORED IN THE FIRST WORK OF THE 4K         00073
*                 CALLER WORK AREA. IT IS FREEMAINED WHEN THE           00074
*                 RETURN CODE OF 16 SIGNALS THE END OF DATASETS         00075
*                 UNDER THIS INDEX.                                     00076
*              5) THE 265 BYTE CAMLIST WORK AREA IS PUT IN THE          00077
*                 CALLER PROVIDED WORK AREA AT OFFSET 4. THIS           00078
*                 ALLOWS ACCESS TO THE COMPLETE VOLUME LIST.            00079
         EJECT                                                          00080
LOCINDEX CSECT                                                          00081
         USING *,15                                                     00082
         SAVE  (14,12),,*                                               00083
         GETMAIN R,LV=LSECT                                             00084
         ST    R13,4(R1)                                                00085
         ST    R1,8(13)                                                 00086
         LR    R12,R13                                                  00087
         LR    R13,R1                                                   00088
         L     R1,24(R12)                                               00089
         BALR  R12,0                                                    00090
         USING *,12                                                     00091
         USING DSASECT,R13                                              00092
R0       EQU   0                                                        00093
R1       EQU   1                                                        00094
R2       EQU   2                                                        00095
R3       EQU   3                                                        00096
R4       EQU   4                                                        00097
R5       EQU   5                                                        00098
R6       EQU   6                                                        00099
R7       EQU   7                                                        00100
R8       EQU   8                                                        00101
R9       EQU   9                                                        00102
R10      EQU   10                                                       00103
R11      EQU   11                                                       00104
R12      EQU   12                                                       00105
R13      EQU   13                                                       00106
R14      EQU   14                                                       00107
R15      EQU   15                                                       00108
         ST    R1,INPARM                                                00109
         L     R10,12(R1)                                               00110
         USING WORKSECT,R10                                             00111
         L     R2,0(R1)                                                 00112
         CLC   OINDEX,0(R2)   SAME INDEX STRUCTURE AS LAST CALL?        00113
         BE    CAMLOC         GO GET NEXT DATASET                       00114
         MVC   OINDEX,0(R2)                                             00115
         SPACE 10                                                       00116
BLDKEY   DS    0H                                                       00117
*        THIS BLOCK ANALIZES THE NEW INDEX STRUCTURE INTO COMPONENTS    00118
*        IT CONSTRUCTS A VSAM CATALOG GENERIC SEARCH KEY AND AN         00119
*        OPTIONAL LOWER LEVEL QUALIFIER VERIFICATION STRING             00120
*        ONE ASTERISK IS ALLOWED ANYWHERE BELOW THE USERID.             00121
*        IT MAY REPRESENT ALL OR PART OF AN 'INDEX LEVEL'               00122
*        EXITS: NORMAL TO 'GENLOC'                                      00123
*               TO 'ERROR4' IF A SYSTAX ERROR OCCURS IN INDEX STRUCTURE 00124
         SR    R1,R1                                                    00125
         SR    R2,R2          CLEAR FOR TRT INSTRUCTION                 00126
         MVI   TRTAB,0                                                  00127
         MVC   TRTAB+1(255),TRTAB                                       00128
         MVI   TRTAB+C' ',4                                             00129
         MVI   TRTAB+C'*',8                                             00130
         MVI   TRTAB+C'.',12                                            00131
         MVC   KEY,OINDEX     COPY INDEX INPUT                          00132
         TRT   KEY,TRTAB                                                00133
         BZ    ERROR4         MUST BE A BLANK IN 44 CHARS               00134
         B     *(R2)                                                    00135
         B     USERID         C' ' MUST BE A USERID                     00136
         B     ERROR4         C'*' ASTERISK MUST FOLLOW PERIOD          00137
         B     COMPLEX        C'.' MUST DO FULL ANALYSIS OF STRUCTURE   00138
         SPACE 5                                                        00139
USERID   DS    0H                                                       00140
*        THIS BLOCK BUILDS SEARCH KEY FOR USERID. MUST BE <9 CHARACTERS 00141
*        AND WE ADD A PERIOD TO IT TO INDICATE TO GENERIC LOCATE        00142
*        THAT WE ARE INTERESTED IN THE DATASETS UNDER THE NAME AND      00143
*        NOT THE ALIAS RECORD (CVOL POINTER) OF THE NAME ITSELF.        00144
         MVI   0(R1),C'.'                                               00145
         LA    R3,KEY                                                   00146
         SR    R1,R3                                                    00147
         CH    R1,=H'8'                                                 00148
         BH    ERROR4                                                   00149
         LA    R1,1(R1)                                                 00150
         STC   R1,NAME        STORE KEY LENGTH                          00151
         MVI   LREST,X'80'                                              00152
         B     GENLOC                                                   00153
* END OF USERID                                                         00154
         SPACE 5                                                        00155
COMPLEX  DS    0H                                                       00156
*        NOW WE SEARCH FOR AN IMBEDDED ASTERISK IN INDEX STRUCTURE      00157
*        CHARACTERS ABOVE IT ARE USED AS THE GENERIC KEY.               00158
*        CHARACTERS AFTER IT ARE USED AS THE LOWER LEVEL QUALIFIERS.    00159
         MVI   TRTAB+C'.',0   NO LONGER INTERESTED IN PERIODS           00160
         TRT   KEY,TRTAB                                                00161
         BZ    ERROR4                                                   00162
         B     *(R2)                                                    00163
         B     ONEPART        NO ASTERISK                               00164
         B     TWOPART                                                  00165
         SPACE 5                                                        00166
ONEPART  MVI   LREST,X'80'                                              00167
         LA    R3,KEY                                                   00168
         SR    R1,R3                                                    00169
         STH   R1,LKEY                                                  00170
         STC   R1,NAME                                                  00171
         B     GENLOC                                                   00172
         SPACE 5                                                        00173
TWOPART  DS    0H                                                       00174
*        THIS BLOCK HANDLES THE CASE WHERE THERE IS AN EMBEDDED         00175
*        ASTERISK IN THE INDEX LEVEL. THE CHARACTERS BELOW THE          00176
*        ASTERISK ARE MOVED TO 'REST'. THE CHARACTERS FROM THE          00177
*        ASTERISK ON ARE BLANKED IN 'KEY'. THE LENGTH OF THE            00178
*        NON BLANK PART OF 'REST' LESS ONE IS SAVED IN 'LREST'          00179
*        FOR USE IN THE LATER CLC INSTRUCTION. THE LENGTH OF            00180
*        THE KEY IS SAVED IN 'NAME' FOR THE GENERIC LOCATE AND          00181
*        IN 'LKEY' FOR AN INDICATION OF HOW MANY CHARS TO               00182
*        SKIP BEFORE LOOKING IN A DSN FOR A MATCH TO 'REST'.            00183
         LA    R3,KEY+42                                                00184
         SR    R3,R1         GET LENGTH OF KEY ABOVE ASTERISK           00185
         MVC   REST,=CL44' ' INITIALIZE REST                            00186
         EX    R3,MOVREST    MOVE PART BELOW ASTERISK                   00187
         LA    R3,1(R3)                                                 00188
         EX    R3,BLNKEY     BLANK KEY FROM ASTERISK ON                 00189
         SH    R3,=H'43'                                                00190
         LPR   R3,R3         GET CHARS ABOVE ASTERISK                   00191
         STH   R3,LKEY                                                  00192
         STC   R3,NAME                                                  00193
         TRT   REST,TRTAB    FIND NON-BLANK LENGTH OF REST              00194
         B     *(R2)                                                    00195
         B     LENRST                                                   00196
         B     ERROR4        SORRY, ONLY ONE * PER CUSTOMER             00197
LENRST   LA    R3,REST+1     CALCULATE LENGTH FROM ADDRESS              00198
         SR    R1,R3            OF FIRST BLANK                          00199
         STH   R1,LREST                                                 00200
         B     GENLOC                                                   00201
MOVREST  MVC   REST(0),1(R1)                                            00202
BLNKEY   MVC   0(0,R1),=CL44' '                                         00203
* END OF TWOPART                                                        00204
* END OF COMPLEX                                                        00205
* END OF BLDKEY                                                         00206
         EJECT                                                          00207
GENLOC   DS    0H                                                       00208
*        THIS BLOCK ISSUES A VSAM GENERIC LOCATE TO BUILD AN IN-CORE    00209
*        LIST OF DATASETS THAT BEGIN WITH THE SEARCH KEY                00210
*        THERE IS NO GOOD DOCUMENTATION ON THE GENERIC                  00211
*        LOCATE IN THE LITERATURE. SEE THE CATALOG PLM FOR              00212
*        WHAT LITTLE THERE IS. THE FORM BELOW WAS INFERRED              00213
*        FROM SOME FICHE AND BY INTERCEPTING SVC 26 WITH DSS.           00214
         MVC   GENFLAG,=X'05201100'                                     00215
         XC    GENX1,GENX1                                              00216
         XC    GENX2,GENX2                                              00217
         LA    R1,NAME                                                  00218
         ST    R1,GENNAME                                               00219
         GETMAIN R,LV=X'7FF8'                                           00220
         ST    R1,GETADDR                                               00221
         ST    R1,GENWORK                                               00222
         MVC   0(4,R1),=X'7FF80004'                                     00223
         LA    R1,GENPARM                                               00224
         SVC   26                                                       00225
         LTR   R15,R15                                                  00226
         BNZ   ERROR4                                                   00227
         L     R1,GETADDR    GET VSAM CATLG RETURN AREA                 00228
         LA    R1,0(R1)       CLEAR HIGH BYTE                           00229
         LH    R2,2(R1)       GET NUMBER BYTES USED                     00230
         AR    R2,R1          ADD START ADDR                            00231
         ST    R2,LAST        SAVE LAST BYTE ADDR                       00232
         LA    R1,49(R1)      SKIP 4 BYTE PREFIX AND FIRST 45 BYTE ENTR 00233
         ST    R1,NEXT        SAVE ADDR OF FIRST DSN ENTRY              00234
* END OF GENLOC                                                         00235
         SPACE 10                                                       00236
CAMLOC   DS    0H                                                       00237
*        NOW DO A REGULAR CAMLIST NAME LOCATE FOR THE NEXT NON-VSAM     00238
*        DATASET IN THE INCORE LIST                                     00239
         XC    CAMLST(16),CAMLST                                        00240
         LA    R1,KEY                                                   00241
         ST    R1,CAMLST+4                                              00242
         LA    R1,VOLCNT                                                00243
         ST    R1,CAMLST+12                                             00244
         L     R1,NEXT                                                  00245
TEST     C     R1,LAST                                                  00246
         BNL   RET16          NO MORE DATASETS IN LIST                  00247
         CLI   0(R1),C'A'     IS IT NON-VSAM                            00248
         BE    S1                                                       00249
NOGO     LA    R1,45(R1)                                                00250
         B     TEST                                                     00251
S1       TM    LREST,X'80'    IS THERE A LOWER QUALIFIER                00252
         BO    S2             NO, SO GO LOCATE                          00253
         SPACE 5                                                        00254
TESTQUAL DS    0H                                                       00255
*        THIS CODE LOOKS FOR THE CHARACTER STRING BELOW THE             00256
*        ASTERISK IN THE INDEX SEARCH KEY. THIS STRING MUST             00257
*        BE SOMEWHERE IN THE DATASET NAME BELOW THE KEY OR              00258
*        THE NAME IS REJECTED                                           00259
         LA    R2,1(R1)                                                 00260
         AH    R2,LKEY        SKIP GENERIC KEY CHARS                    00261
         LH    R3,LREST       GET # CHARS-1 IN REST                     00262
         LA    R4,44                                                    00263
         SR    R4,R3                                                    00264
         SH    R4,LKEY        R4=# CHARS BELOW KEY + 1                  00265
COMP     EX    R3,COMPAR                                                00266
         BE    S2                                                       00267
         LA    R2,1(R2)                                                 00268
         BCT   R4,COMP                                                  00269
         B     NOGO                                                     00270
COMPAR   CLC   REST(0),0(R2)                                            00271
* END OF TESTQUAL                                                       00272
         SPACE 5                                                        00273
S2       DS    0H                                                       00274
*        NOW TO ISSUE NORMAL LOCATE BY NAME AND RETURN INFORMATION      00275
*        ACCORDING TO DESCRIPTION OF PARAMETERS IN LEADING DOCUMENT     00276
         MVC   KEY,1(R1)      COPY DSN                                  00277
         LA    R1,45(R1)      PT TO NXT DSN                             00278
         ST    R1,NEXT        SAVE FOR NXT TIME                         00279
         LA    R1,CAMLST                                                00280
         SVC   26             LOCATE BY NAME                            00281
         L     R2,INPARM      NOW GET PASSED PARM LIST                  00282
         LM    R2,R3,4(R2)    PICK UP 2ND & 3RD USER PARMS              00283
         MVC   0(44,R2),KEY   SAVE DSN                                  00284
         MVC   0(6,R3),SER    SAVE VOL                                  00285
         TM    DEVT+2,X'20'   IS THIS DISK                              00286
         BZ    ERROR8         NO, PASS 8 RETURN CODE                    00287
         CLI   VOLCNT+1,1     IS IT MULTI-VOL                           00288
         BNE   ERROR12        YES, PASS 12 RETURN CODE                  00289
* END OF CAMLOC                                                         00290
         SPACE 10                                                       00291
*VARIOUS RETURNS                                                        00292
RETN     SR    R15,R15                                                  00293
         B     RC                                                       00294
ERROR4   LA    R15,4                                                    00295
         B     RC                                                       00296
ERROR8   LA    R15,8                                                    00297
         B     RC                                                       00298
ERROR12  LA    R15,12                                                   00299
         B     RC                                                       00300
RET16    FREEMAIN R,LV=X'7FF8',A=GETADDR                                00301
         LA    R15,16                                                   00302
         B     RC                                                       00303
ERROR20  LA    R15,20                                                   00304
RC       L     R1,INPARM                                                00305
         TM    12(R1),X'80'                                             00306
         BO    EXIT                                                     00307
         L     R1,16(R1)                                                00308
         ST    R15,0(R1)                                                00309
EXIT     L     R13,4(R13)                                               00310
         ST    R15,16(R13)                                              00311
         L     R1,8(R13)                                                00312
         FREEMAIN R,LV=LSECT,A=(1)                                      00313
         LM    R14,R12,12(R13)                                          00314
         BR    R14                                                      00315
         EJECT                                                          00316
DSASECT  DSECT                                                          00317
         DS    18A                                                      00318
GENPARM  DS    0F            GENERIC LOCATE PARM AREA                   00319
GENFLAG  DS    F                                                        00320
GENNAME  DS    A                                                        00321
GENX1    DS    A                                                        00322
GENWORK  DS    A                                                        00323
GENX2    DS    3A                                                       00324
KEYLEN   DS    0H                                                       00325
         DS    X                                                        00326
NAME     DS    X                                                        00327
KEY      DS    CL44                                                     00328
INPARM   DS    A             SAVE R1 UPON ENTRY TO LOCINDEX             00329
CAMLST   DS    4A                                                       00330
         DS    0D                                                       00331
LSECT    EQU   *-DSASECT                                                00332
         SPACE 10                                                       00333
GETSECT  DSECT               GETMAINED VSAM WORK AREA                   00334
GETLEN   DS    H             LENGT OF AREA                              00335
GETUSED  DS    H             AMOUNT ALLOCATED CURRENTLY TO DATA         00336
GETENTY  DS    0CL45         FIRST ELEMENT OF DSN ARRAY                 00337
GETTYPE  DS    C             TYPE FLAG ('A'=NONVSAM)                    00338
GETNAME  DS    CL44          DSN                                        00339
         SPACE 10                                                       00340
WORKSECT DSECT               WORK AREA PASSED AS PARM                   00341
GETADDR  DS    A             ADDRESS OF GETMAINED VSAM WORK AREA        00342
TRTAB    DS    0CL256                                                   00343
VOLCNT   DS    H                                                        00344
DEVT     DS    XL4                                                      00345
SER      DS    CL6                                                      00346
SEQ      DS    H                                                        00347
         DS    CL251                                                    00348
OINDEX   DS    CL44          PREVIOUS VALUE OF FIRST PARM               00349
REST     DS    CL44          LOW QUALIFIER COMPARE STRING               00350
NEXT     DS    A             ADDRESS OF NEXT DSN IN INCORE TABLE        00351
LAST     DS    A             ADDRESS OF BYTE AFTER END OF DSNLIST       00352
LKEY     DS    H             LENGTH OF GENERIC KEY                      00353
NOREST   DS    0B            FLAG IF 'REST' IS EMPTY                    00354
LREST    DS    H             LENGTH OF REST                             00355
         END                                                            00356
